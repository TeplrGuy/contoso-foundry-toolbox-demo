targetScope = 'subscription'

@minLength(1)
@maxLength(64)
@description('Name of the environment which is used to generate a short unique hash used in all resources.')
param environmentName string

@minLength(1)
@description('Primary location for all resources. Must support Azure Functions Flex Consumption, Microsoft.Web connectorGateways, and the default Microsoft Foundry gpt-5.4 Global Standard deployment.')
@allowed([
  'centralus'
  'eastus'
  'eastus2'
  'northcentralus'
  'southcentralus'
  'westus'
])
@metadata({
  azd: {
    type: 'location'
  }
})
param location string

@description('Location for the Azure AI Search service backing the study-doc toolbox tool. Search is reached over its public HTTPS endpoint and does not need to be co-located with the Function App or Foundry project, so it is kept separate from `location` to allow placing it in a region with available Search capacity.')
@allowed([
  'canadacentral'
  'canadaeast'
  'centralus'
  'eastus'
  'eastus2'
  'northcentralus'
  'southcentralus'
  'westus'
])
param searchLocation string = 'canadacentral'

@description('Microsoft Foundry model deployment name.')
param foundryModel string = 'gpt-5.4'

@description('Microsoft Foundry model name.')
param foundryModelName string = 'gpt-5.4'

@description('Microsoft Foundry model version.')
param foundryModelVersion string = '2026-03-05'

@description('Microsoft Foundry deployment capacity.')
param foundryDeploymentCapacity int = 50

@description('Reasoning effort for supported Foundry reasoning models.')
@allowed([
  'none'
  'low'
  'medium'
  'high'
  'xhigh'
])
param reasoningEffort string = 'high'

@description('Reasoning summary mode for supported Foundry reasoning models.')
@allowed([
  'auto'
  'concise'
  'detailed'
])
param reasoningSummary string = 'concise'

@description('Email address to send the daily Azure report to.')
param toEmail string

@description('Optional managed identity client ID to use when authenticating to the Office 365 Outlook MCP server. Leave empty to use the app-wide identity selection.')
param o365McpClientId string = ''

var abbrs = loadJsonContent('./abbreviations.json')
var resourceToken = toLower(uniqueString(subscription().id, environmentName, location))
var tags = { 'azd-env-name': environmentName }
var functionAppName = '${abbrs.webSitesFunctions}agent-func-${resourceToken}'
var searchServiceName = '${abbrs.searchSearchServices}medpace-${resourceToken}'
var searchConnectionName = 'medpace-aisearch-conn'
var toolboxName = 'medpace-study-toolbox'
var toolboxFlatName = 'medpace-study-toolbox-flat'
var foundryAccountName = 'cog-${resourceToken}'
var foundryProjectName = '${foundryAccountName}-proj'
var deploymentStorageContainerName = 'app-package-${take(functionAppName, 32)}-${take(toLower(uniqueString(functionAppName, resourceToken)), 7)}'
var deployerPrincipalId = deployer().objectId
var connectorGatewayName = 'cg-${resourceToken}'
var office365ConnectionName = 'office365-outlook'
var office365McpServerConfigName = 'Office-365-Outlook-send-email-only'

// Reader role definition ID
var readerRoleId = 'acdd72a7-3385-48ef-bd42-f606fba81ae7'

// Resource Group
resource rg 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: '${abbrs.resourcesResourceGroups}${environmentName}'
  location: location
  tags: tags
}

// User Assigned Managed Identity
module apiUserAssignedIdentity 'br/public:avm/res/managed-identity/user-assigned-identity:0.4.1' = {
  name: 'apiUserAssignedIdentity'
  scope: rg
  params: {
    location: location
    tags: tags
    name: '${abbrs.managedIdentityUserAssignedIdentities}agent-func-${resourceToken}'
  }
}

// Microsoft Foundry
module foundry './app/foundry.bicep' = {
  name: 'foundry'
  scope: rg
  params: {
    accountName: foundryAccountName
    projectName: foundryProjectName
    location: location
    tags: tags
    modelDeploymentName: foundryModel
    modelName: foundryModelName
    modelVersion: foundryModelVersion
    deploymentCapacity: foundryDeploymentCapacity
    managedIdentityPrincipalId: apiUserAssignedIdentity.outputs.principalId
  }
}

// Azure AI Search for the Medpace study lookup demo
module search './app/search.bicep' = {
  name: 'search'
  scope: rg
  params: {
    name: searchServiceName
    location: searchLocation
    tags: tags
    readerPrincipalIds: [
      apiUserAssignedIdentity.outputs.principalId
      foundry.outputs.projectPrincipalId
      foundry.outputs.accountPrincipalId
    ]
  }
}

// Foundry connection to Azure AI Search, consumed by the toolbox azure_ai_search tool
module searchConnection './app/search-connection.bicep' = {
  name: 'searchConnection'
  scope: rg
  params: {
    foundryAccountName: foundry.outputs.accountName
    connectionName: searchConnectionName
    searchEndpoint: search.outputs.endpoint
    searchResourceId: search.outputs.resourceId
    searchLocation: searchLocation
  }
}

// Office 365 Outlook v2 connection exposed through an MCP server
module office365Connector './app/connector-gateway.bicep' = {
  name: 'office365Connector'
  scope: rg
  params: {
    connectorGatewayName: connectorGatewayName
    connectionName: office365ConnectionName
    mcpServerConfigName: office365McpServerConfigName
    location: location
    tags: tags
    managedIdentityPrincipalId: apiUserAssignedIdentity.outputs.principalId
    deployerPrincipalId: deployerPrincipalId
    tenantId: tenant().tenantId
  }
}

// App Service Plan (Flex Consumption)
module appServicePlan 'br/public:avm/res/web/serverfarm:0.1.1' = {
  name: 'appserviceplan'
  scope: rg
  params: {
    name: '${abbrs.webServerFarms}${resourceToken}'
    sku: {
      name: 'FC1'
      tier: 'FlexConsumption'
    }
    reserved: true
    location: location
    tags: tags
  }
}

// Function App
module api './app/api.bicep' = {
  name: 'api'
  scope: rg
  params: {
    name: functionAppName
    location: location
    tags: tags
    applicationInsightsName: monitoring.outputs.name
    appServicePlanId: appServicePlan.outputs.resourceId
    runtimeName: 'python'
    runtimeVersion: '3.13'
    storageAccountName: storage.outputs.name
    deploymentStorageContainerName: deploymentStorageContainerName
    identityId: apiUserAssignedIdentity.outputs.resourceId
    identityClientId: apiUserAssignedIdentity.outputs.clientId
    appSettings: {
      AZURE_FUNCTIONS_AGENTS_PROVIDER: 'foundry'
      FOUNDRY_PROJECT_ENDPOINT: foundry.outputs.projectEndpoint
      FOUNDRY_MODEL: foundry.outputs.modelDeploymentName
      FOUNDRY_TOOLBOX_MCP_URL: '${foundry.outputs.projectEndpoint}/toolboxes/${toolboxName}/mcp?api-version=v1'
      FOUNDRY_TOOLBOX_FLAT_MCP_URL: '${foundry.outputs.projectEndpoint}/toolboxes/${toolboxFlatName}/mcp?api-version=v1'
      AZURE_FUNCTIONS_AGENTS_REASONING_EFFORT: reasoningEffort
      AZURE_FUNCTIONS_AGENTS_REASONING_SUMMARY: reasoningSummary
      AZURE_CLIENT_ID: apiUserAssignedIdentity.outputs.clientId
      TO_EMAIL: toEmail
      SUBSCRIPTION_ID: subscription().subscriptionId
      O365_MCP_SERVER_URL: office365Connector.outputs.mcpEndpointUrl
      O365_MCP_CLIENT_ID: o365McpClientId
      ENABLE_MULTIPLATFORM_BUILD: 'true'
      PYTHON_ENABLE_INIT_INDEXING: '1'
    }
  }
}

// Storage Account
module storage 'br/public:avm/res/storage/storage-account:0.8.3' = {
  name: 'storage'
  scope: rg
  params: {
    name: '${abbrs.storageStorageAccounts}${resourceToken}'
    allowBlobPublicAccess: false
    allowSharedKeyAccess: true
    dnsEndpointType: 'Standard'
    publicNetworkAccess: 'Enabled'
    networkAcls: {
      defaultAction: 'Allow'
      bypass: 'AzureServices'
    }
    blobServices: {
      containers: [{ name: deploymentStorageContainerName }]
    }
    minimumTlsVersion: 'TLS1_2'
    location: location
    tags: tags
  }
}

// RBAC — storage, app insights
module rbac './app/rbac.bicep' = {
  name: 'rbacAssignments'
  scope: rg
  params: {
    storageAccountName: storage.outputs.name
    appInsightsName: monitoring.outputs.name
    managedIdentityPrincipalId: apiUserAssignedIdentity.outputs.principalId
  }
}

// RBAC — Reader on subscription for azure_rest custom tool
resource subscriptionReaderRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: guid(subscription().id, functionAppName, readerRoleId)
  properties: {
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', readerRoleId)
    principalId: apiUserAssignedIdentity.outputs.principalId
    principalType: 'ServicePrincipal'
  }
}

// Log Analytics
module logAnalytics 'br/public:avm/res/operational-insights/workspace:0.7.0' = {
  name: '${uniqueString(deployment().name, location)}-loganalytics'
  scope: rg
  params: {
    name: '${abbrs.operationalInsightsWorkspaces}${resourceToken}'
    location: location
    tags: tags
    dataRetention: 30
  }
}

// Application Insights
module monitoring 'br/public:avm/res/insights/component:0.4.1' = {
  name: '${uniqueString(deployment().name, location)}-appinsights'
  scope: rg
  params: {
    name: '${abbrs.insightsComponents}${resourceToken}'
    location: location
    tags: tags
    workspaceResourceId: logAnalytics.outputs.resourceId
    disableLocalAuth: true
  }
}

// Outputs
output AZURE_LOCATION string = location
output AZURE_FUNCTION_NAME string = api.outputs.SERVICE_API_NAME
output AZURE_SEARCH_SERVICE_NAME string = search.outputs.name
output AZURE_SEARCH_LOCATION string = searchLocation
output AZURE_SEARCH_INDEX string = 'study-docs'
output FOUNDRY_SEARCH_CONNECTION_NAME string = searchConnection.outputs.connectionName
output FOUNDRY_ACCOUNT_NAME string = foundry.outputs.accountName
output FOUNDRY_TOOLBOX_MCP_URL string = '${foundry.outputs.projectEndpoint}/toolboxes/${toolboxName}/mcp?api-version=v1'
output FOUNDRY_TOOLBOX_FLAT_MCP_URL string = '${foundry.outputs.projectEndpoint}/toolboxes/${toolboxFlatName}/mcp?api-version=v1'
output AZURE_SEARCH_ENDPOINT string = search.outputs.endpoint
output FOUNDRY_PROJECT_ENDPOINT string = foundry.outputs.projectEndpoint
output FOUNDRY_MODEL string = foundry.outputs.modelDeploymentName
output O365_CONNECTOR_GATEWAY_NAME string = office365Connector.outputs.connectorGatewayName
output O365_CONNECTION_ID string = office365Connector.outputs.connectionId
output O365_MCP_SERVER_URL string = office365Connector.outputs.mcpEndpointUrl
